plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "net.covers1624.signing" version '1.1.4'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

group = "com.brandon3055.draconicevolution"
archivesBaseName = "Draconic-Evolution"

sourceSets.main.resources.srcDirs += "src/main/generated"

file('build.properties').withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def signProps = [:]
if (System.getenv("KEY_STORE")) {
    println "Using Env variables for jar signing."
    signProps['keyStore'] = System.getenv("KEY_STORE")
    file(System.getenv("KEY_STORE_PROPS")).withReader {
        def props = new Properties()
        props.load(it)
        signProps.putAll(props)
    }
} else if (project.hasProperty('keyStore')) {
    println "Using Project properties for jar signing."
    signProps['keyStore'] = project.getProperty('keyStore')
    signProps['storePass'] = project.getProperty('keyStorePass')
    signProps['alias'] = project.getProperty('keyStoreAlias')
    signProps['keyPass'] = project.getProperty('keyStoreKeyPass')
} else {
    println 'No signing secrets found, build will not be signed.'
}

config.mod_version = "${config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "9999")
version = "${config.mc_version}-${config.mod_version}"
println "Starting build of ${archivesBaseName}, Version: ${config.mod_version}"
println "Using Forge: ${config.neo_version}, for Minecraft: ${config.mc_version}"

minecraft {
    mappings channel: 'official', version: config.mc_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        client {
            workingDirectory file('run')
            mods { draconicevolution { source sourceSets.main } }
        }
        server {
            workingDirectory file('run')
            mods { draconicevolution { source sourceSets.main } }
        }
        data {
            workingDirectory file('run')
            args '--mod', 'draconicevolution', '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods { draconicevolution { source sourceSets.main } }
        }
    }
}

mixin {
    add(sourceSets.main, "mixins.draconicevolution.refmap.json")
}

processResources {
    inputs.property 'mod_version', config.mod_version
    inputs.property 'mc_version', config.mc_version
    inputs.property 'ccl_version', config.ccl_version
    inputs.property 'bcore_version', config.bcore_version

    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': config.mod_version],
                'mc_version': config.mc_version,
                'neo_version': config.neo_version,
                'pi_version': config.pi_version,
                'bcore_version': config.bcore_version,
                'cct_version': config.cct_version,
                'lang_version': config.neo_version.split('\\.')[0],
                'ccl_version': "[${config.ccl_version.replace(".+", "")},${config.ccl_version_max})"
    }
}

configurations {
//    shadow
//    implementation.extendsFrom shadow
    annotationProcessor.extendsFrom minecraft
}


repositories {
    flatDir { dirs 'libs' }
    mavenLocal()
    maven { url = "https://maven.covers1624.net/" }
    maven { url = "https://dvs1.progwml6.com/files/maven" }
    maven { url = "https://maven.cil.li/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://squiddev.cc/maven/" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://modmaven.dev/" }
}

dependencies {
    minecraft "net.neoforged:forge:${config.mc_version}-${config.neo_version}"

    implementation fg.deobf("com.brandon3055.brandonscore:BrandonsCore:${config.mc_version}-${config.bcore_version}:universal")
    implementation fg.deobf("io.codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:universal")
    implementation fg.deobf("io.codechicken:EnderStorage:${config.mc_version}-${config.ender_storage_version}:universal")
    compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${config.mc_version}:${config.crt_version}")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly(fg.deobf("mezz.jei:jei-${config.mc_version}-common-api:${config.jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${config.mc_version}-forge-api:${config.jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${config.mc_version}-forge:${config.jei_version}"))

    compileOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${config.curios_version}:api"))
    runtimeOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${config.curios_version}"))

    compileOnly("cc.tweaked:cc-tweaked-${config.mc_version}-core-api:${config.cct_version}")
    compileOnly(fg.deobf("cc.tweaked:cc-tweaked-${config.mc_version}-forge-api:${config.cct_version}"))
    implementation(fg.deobf("cc.tweaked:cc-tweaked-${config.mc_version}-forge:${config.cct_version}"))

    // For Testing
//    runtimeOnly fg.deobf("mekanism:Mekanism:1.18.2-10.2.5.465")// core
//    runtimeOnly fg.deobf("mekanism:Mekanism:1.18.2-10.2.5.465:additions")// Mekanism: Additions
//    runtimeOnly fg.deobf("mekanism:Mekanism:1.18.2-10.2.5.465:generators")// Mekanism: Generators
//    runtimeOnly fg.deobf("mekanism:Mekanism:1.18.2-10.2.5.465:tools")// Mekanism: Tools
}

test {
    useJUnitPlatform()
}

signing {
    if (!signProps.isEmpty()) {
        jars {
            sign jar
            after 'reobfJar'

            keyStore = signProps.keyStore
            alias = signProps.alias
            storePass = signProps.storePass
            keyPass = signProps.keyPass
        }
    }
}

def commonManifest = {
    attributes 'Specification-Title': archivesBaseName
    attributes 'Specification-Vendor': 'brandon3055'
    attributes 'Specification-Version': "1"
    attributes 'Implementation-Title': archivesBaseName
    attributes 'Implementation-Vendor': 'brandon3055'
    attributes 'Implementation-Version': version
    attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    attributes 'MixinConfigs': 'mixins.draconicevolution.json'
}



jar {
    finalizedBy 'reobfJar'
    archiveClassifier = 'universal'
    from file("LICENSE")
    manifest commonManifest
}

task srcJar(type: Jar) {
    build.dependsOn it
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    from file("LICENSE")
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'brandon3055'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        DraconicEvolution(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact srcJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                //The publish plugin doesnt like GString's here apparently..
                url = "https://github.com/brandon3055/${archivesBaseName}".toString()
                scm {
                    url = "https://github.com/brandon3055/${archivesBaseName}".toString()
                    connection = "scm:git:git://github.com/brandon3055/${archivesBaseName}.git".toString()
                    connection = "scm:git:git@github.com:brandon3055/${archivesBaseName}.git".toString()
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/brandon3055/${archivesBaseName}/issues".toString()
                }
                licenses {
                    license {
                        name = "CoFH \"Don't Be a Jerk\" License"
                        url = "https://github.com/brandon3055/Draconic-Evolution/blob/master/LICENSE"
                        distribution = 'Repo'
                    }
                }
                developers {
                    developer {
                        id = 'brandon3055'
                        name = 'brandon3055'
                    }
                }
            }
        }
    }
}


/**
 * Polls the 'compile' configuration for a dependency with the given module name
 * and resolves, and returns its version. E.g: '1.2.+' will resolve to '1.2.3.4'
 *
 * @param module The module to search for.
 * @param chop If the string should be chopped on the last '-' in its string.
 * @param configuration The name of the configuration to search.
 * @param errorMissing If an error should be thrown if it can't be found.
 * @return The version string, '0' if 'errorMissing' is false and nothing was found.
 */
def resolve(module, chop = true, configuration = 'compile', errorMissing = true) {
    //Copy and lenient resolve the configuration, Forge cant be resolved at this time so lenient is required.
    def filtered = configurations.getByName(configuration).copy().incoming.artifactView({ it.lenient = true }).artifacts
            .findAll { it.id.componentIdentifier.module == module }
            .collect { it.id.componentIdentifier.version }
    if (filtered.size() > 1) {
        println "WARNING: Found ${filtered.size()} Dependencies with ModuleName '${module}' in configuration '${configuration.name}'"
    }
    if (errorMissing && filtered.isEmpty()) {
        throw new RuntimeException("Failed resolve dependency version for '${module}'")
    }
    if (filtered.isEmpty()) return "0"
    def version = filtered.first() as String
    if (chop) {
        def idx = version.lastIndexOf('-')
        return version.substring(idx + 1)
    }
    return version
}
